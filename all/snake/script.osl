save "snake@mistium" "set_directory"

if "highscore.txt".saveExists() (
  save "highscore.txt" "get"
  highscore = "highscore.txt".saveGet() ?? 0
)

def "move" "x, y" (
  x += head_x
  y += head_y
  void snakex.append(x)
  void snakey.append(y)
)

t = 0
snakex = [0]
snakey = [0]
snake_dir = 0
mode = "Menu"
window "resizable" false
window "dimensions" 400 430
score = 0
slider_speed = 0.2

mainloop:
c #222
loc 999 2 0 -20
square window_width 10 30
c #fff
loc 2 2 10 -20
text `Snake | Highest: ${highscore}` 9

if mode == "Menu" (
  c #777
  goto 0 0
  square 150 20 10 1
  snakelen = 1
  snakex = ["100"]
  snakey = ["-10"]
  if clicked (
    mode = "play"
    snakedir = "w"
    head_x = 0
    head_y = -20
    move 0 20
    foodx = random(-9,9) * 20
    foody = random(-9,9) * 20 - 20
  )
)
if mode == "menu" (
  c #fff
  change_x -20
  text "Play" 9
)
if mode == "Play" (
  if highscore < snakelen (
    highscore = snakelen
    save "highscore.txt" "set" highscore
  )
  if "w".onKeyDown() and snakedir != "s" (
    snakedir = "w"
  )
  if "s".isKeyDown() and snakedir != "w" (
    snakedir = "s"
  )
  if "a".isKeyDown() and snakedir != "d" (
    snakedir = "a"
  )
  if "d".isKeyDown() and snakedir != "a" (
    snakedir = "d"
  )

  if timer - slider_speed > t (
    t = timer
    if snakedir == "w" (
      move 0 20
    )
    if snakedir == "a" (
      move -20 0
    )
    if snakedir == "s" (
      move 0 -20
    )
    if snakedir == "d" (
      move 20 0
    )
  )
  temp = snakex.len
  head_x = snakex[temp].toNum()
  head_y = snakey[temp].toNum()
  count = temp + 1
  loop snakelen (
    count --
    draw_x = snakex[count].toNum()
    draw_y = snakey[count].toNum()
    goto draw_x draw_y
    if draw_x == head_x and draw_y == head_y and count < snakex.len (
      mode = "menu"
    )
    c #65ad5b
    if snakex.len - count % 2 == 0 (
      c #416f3b
    )
    if count == snakex.len (
      c #cd0000
    )
    square 13 13 6 1
  )

  if head_x > 180 or head_x < -180 or head_y > 160 or head_y < -210 (
    mode = "menu"
  )
  c #cd0000
  goto foodx foody
  square 5 5 10
  if head_x == foodx and head_y == foody (
    snakelen ++
    foodx = random(-9, 9) * 20
    foody = random(-9, 9) * 20 - 20
  )
)
import "win-buttons"
